class GhostSquadSpell : ArcanumSpell
{
	override int GetIndex() { return 5; }
	override class<ArcanumSpellTree> GetTree() { return 'ArcanumTreeConjuration'; }
	override string GetName() { return "Ghost Squad"; }
	override int GetSpellType() { return SType_Targeted; }
	override int GetManaCost() { return 80; }
	override string GetDescription()
	{
		int count = int(1 + 2 * GetExperienceFactor());
		return String.Format("Summons \c[Gold]%i\c- ghost marine%s at target location.", count, count > 1 ? "s" : "");
	}
	override string GetTechnicalInfo() { return "Targeted"; }
	override int GetMaxExperience() { return 60; }
	override class<ArcanumSigil> GetSigil() { return 'GhostSquadSigil'; }
}

class GhostSquadSigil : ArcanumSigil
{
	override void ActivateTargeted()
	{
		FLineTraceData Data;
		LineTrace(angle, HDCONST_ONEMETRE * 100, pitch, 0, 0, 0, 0, Data);

		vector3 OldPos = master.pos;
		if (Data.HitType != Data.TRACE_HasHitSky)
		{
			master.SetXYZ(Data.HitLocation - Data.HitDir);
			PlayCastSound(self, "Arcanum/Conjuration/GhostSquad");
			master.A_AlertMonsters();

			int count = int(1 + 5 * ExperienceFactor);
			for (int i = 0; i < count; ++i)
			{
				double ang = (360.0 / count) * i;
				master.A_SpawnItemEx("SquadGhost", 0, 0, 0, -8, 0, 0, ang, SXF_NOCHECKPOSITION | SXF_SETMASTER);
				master.A_SpawnItemEx("HDSmoke", 0, 0, 0, -8, 0, 0, ang, SXF_NOCHECKPOSITION);
			}

			string deadawaken;
			switch (random(0, 3))
			{
				case 0: deadawaken = "\cj'They shall stand again and hear there\n\cja horn in the hills ringing.\n\n\cjWhose shall the horn be?'"; break;
				case 1: deadawaken = "\cj'For this war will last through years uncounted\n\n\cjand you shall be summoned once again ere the end.'"; break;
				case 2: deadawaken = "\cj'Faint cries I heard,and dim horns blowing,\n\n\cjand a murmur as of countless far voices:\n\n\n\cjit was like the echo of some forgotten battle\n\n\cjin the Dark Years long ago.'"; break;
				case 3: deadawaken = "\cj'Pale swords were drawn; but I know not\n\n\cjwhether their blades would still bite,\n\n\n\cjfor the Dead needed no longer\n\n\cjany weapon but fear.'"; break;
			}
			A_PrintBold(deadawaken, deadawaken.length() * 0.05, "NewSmallFont");
			master.SetXYZ(OldPos);
		}
	}

	override void InitRuneSlots()
	{
		vector2 Size = (2, 3.33);
		CreateRuneSlot((-8.4, 0), Size, 0.5); CreateRuneSlot((8.4, 0), Size, 0.5);
		CreateRuneSlot((0, -8.4), Size, 0.5); CreateRuneSlot((0, 8.4), Size, 0.5);
	}

	Default
	{
		StencilColor "B4C8A3";
	}

	States
	{
		Spawn:
			CRCN A 0;
			Goto Super::Spawn;
	}
}